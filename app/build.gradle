apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def final appConfig = [
        versions: [
                "support_library": "23.2.0",
                "kotlin_version": "$kotlin_version",
                "support_test": "0.4.1",
                "espresso": "2.2.1"
        ],

        "base_url": "https://www.eslpod.com/website/show_all.php",
        "detail_url": "https://www.eslpod.com/website/show_podcast.php",
        "server_port": 8089
];

def generateVersionCode() {
    try {
        def cmd = 'git rev-list --count HEAD'
        cmd.execute().text.trim().toInteger()
    } catch (ignored) {
        1
    }
}

def generateVersionName() {
    try {
        def cmd = 'git describe --tags --abbrev=0'
        cmd.execute().text.trim()
    } catch (ignored) {
        "1.0"
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "br.com.wakim.eslpodclient"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        resConfigs "en", "pt-rBR"

        buildConfigField("String", "BASE_URL", "\"$appConfig.base_url\"")
        buildConfigField("String", "DETAIL_URL", "\"$appConfig.detail_url\"")

        buildConfigField("int", "SERVER_PORT", "$appConfig.server_port")
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                def versionCode = generateVersionCode() ?: 1
                def versionName = generateVersionName() + "." + versionCode ?: "1"

                variant.mergedFlavor.versionCode = versionCode
                variant.mergedFlavor.versionName = versionName
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url  "http://dl.bintray.com/lukaville/maven"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$appConfig.versions.support_library"
    compile "com.android.support:design:$appConfig.versions.support_library"
    compile "com.android.support:cardview-v7:$appConfig.versions.support_library"
    compile "com.android.support:recyclerview-v7:$appConfig.versions.support_library"
    compile "com.android.support:preference-v7:$appConfig.versions.support_library"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$appConfig.versions.kotlin_version"

    compile 'com.google.dagger:dagger:2.0.1'
    kapt 'com.google.dagger:dagger-compiler:2.0'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.1'

    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'

    compile 'org.jsoup:jsoup:1.8.3'

    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.nbsp:library:1.07'

    compile 'org.nanohttpd:nanohttpd:2.3.0'

    provided 'org.glassfish:javax.annotation:10.0-b28'

    debugCompile 'com.facebook.stetho:stetho:1.3.0'

    testCompile 'junit:junit:4.12'

    // Android Testing Support Library's runner and rules
    androidTestCompile ("com.android.support.test:runner:$appConfig.versions.support_test") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile ("com.android.support.test:rules:$appConfig.versions.support_test") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Espresso UI Testing
    androidTestCompile ("com.android.support.test.espresso:espresso-core:$appConfig.versions.espresso") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:$appConfig.versions.espresso") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'

        exclude module: 'recyclerview-v7'
    }
}

kapt {
    generateStubs = true
}
